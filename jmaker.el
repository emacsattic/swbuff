;;; jmaker.el -- Java Makefile generator add-on to JDE.
;; $Revision: 1.5 $

;; Copyright (C) 1998 by David Ponce

;; Author: David Ponce <david.ponce@wanadoo.fr>
;; Maintainer: David Ponce <david.ponce@wanadoo.fr>
;; Keywords: java, tools

;; This program is free software; you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation; either version 2, or (at your option)
;; any later version.

;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.

;; You should have received a copy of the GNU General Public License
;; along with this program; see the file COPYING.  If not, write to the
;; Free Software Foundation, Inc., 59 Temple Place - Suite 330,
;; Boston, MA 02111-1307, USA.

;;; Description:

;; This package is an add-on to the Java Development Environment
;; (JDE) for Emacs. It automatically generates Makefiles to improve
;; Java projects building using make. The default Makefile template
;; provided uses the java compiler and its options from JDE and generate
;; targets to compile all .java files in the current directory.

;;; Installation:

;; Put the following in your .emacs file:
;;   (require 'jmaker)
;;
;; As jmaker does (require 'jde) you can omit it in your .emacs.

;;; Usage:

;; While editing a Java file use:
;;   M-x jmaker-generate-makefile to generate a new Makefile
;;
;; A new Makefile is generated in a temporary buffer named `Makefile' which
;; can be modified if needed then saved with C-x C-w.
;; CAUTION: jmaker doesn't take back any modifications you could have made
;; to an existing Makefile!
;;
;; To build a project with make you can use the `compile' command or the
;; `jde-build' command if `jde-build-use-make' is set to a non-nil value.

;;; Customization:

;; While editing a Java file use:
;;   M-x jmaker-customize to change jmaker options
;;
;; If the `jmaker-hook-jde-menu' variable is non-nil (the default) the JDE
;; `Generate' and `Options' sub-menus will have respectively a new `Makefile'
;; item to generate a new Makefile and a new `jmaker' item to change jmaker
;; options. The jmaker version number is also added to the JDE `Help' sub-menu.

;;; Support:

;; Any comments, suggestions, bug reports or upgrade requests are welcome.
;; Please send them to David Ponce at david.ponce@wanadoo.fr.
;;
;; This version of jmaker is developed and tested with NTEmacs 19.34.6 and
;; Cygnus GNU-Win32 B18 tools (bash, make) under MS Windows NT 4 WKS SP3.
;; jmaker requires the "jde" package (works with jde 2.0.7, 2.0.8 and
;; probably might work with slightly earlier and later versions).
;; Please, let me know if jmaker work with other OS and versions of GNU Emacs.

;;; Code:

(require 'jde)
(require 'tempo)

(defconst jmaker-version "$Revision: 1.5 $"
  "jmaker version number.")

(defgroup jmaker nil
  "Java Makefile Maker Environment"
  :group 'tools
  :prefix "jmaker-")

(defcustom jmaker-makefile-buffer-template
	'(
		"\"####\" 'n"
		"\"#### Java Makefile automatically generated by jmaker \" (jmaker-version-number) 'n"
		"\"#### Creation date: \" (current-time-string) 'n"
		"\"####\" 'n"
		"'n"
		"\"#### Java compiler settings\" 'n"
		"\"JAVAC       = \" jde-compiler  'n"
		"\"JAVAC_FLAGS = \" (jde-get-compile-options) 'n"
		"'n"
		"\"#### Build Rules\" 'n"
		"\"%.class: %.java\" 'n"
		"\"\\t$(JAVAC) $(JAVAC_FLAGS) $<\" 'n"
		"'n"
		"\"#### Global target\" 'n"
		"\"all:\\t\""
		"(jmaker-all-target)"
		"'n"
		"\"#### Single targets\" 'n"
		"(jmaker-file-targets)"
		"'n"
		"\"#### Cleanup\" 'n"
		"\"clean:\" 'n"
		"\"\\t$(RM) *.class\" 'n"
		"'n"
		"\"#### Rebuild\" 'n"
		"\"build:\\tclean all\" 'n"
		"'n"
		"\"#### Usage\" 'n"
		"\"help:\" 'n"
		"\"\\t@echo \\\"Usage: make [all|<single target>|clean|build|help]\\\"\" 'n"
		"\"\\t@echo \\\"  all             - build all as needed (default)\\\"\" 'n"
		"\"\\t@echo \\\"  <single target> - build the given file as needed\\\"\" 'n"
		"\"\\t@echo \\\"  clean           - remove all .class files\\\"\" 'n"
		"\"\\t@echo \\\"  build           - build all inconditionnally\\\"\" 'n"
		"\"\\t@echo \\\"  help            - display this help\\\"\" 'n"
		"'n"
		)
  "*Template for Java Makefile.
Setting this variable defines a template instantiation
command `jmaker-insert-makefile', as a side-effect."
  :group 'jmaker
  :type '(repeat string)
  :set '(lambda (sym val)
					(defalias 'jmaker-insert-makefile
						(tempo-define-template "jmaker-makefile-buffer-template"
																	 (jde-gen-read-template val)
																	 nil
																	 "Insert a Java Makefile in the current buffer."))
					(set-default sym val)))

(defcustom jmaker-hook-jde-menu t
  "*If on add Makefile items in JDE Generate and Options sub-menus.
The jmaker version number is also added to the JDE Help sub-menu."
  :group 'jmaker
  :type 'boolean)

(defun jmaker-customize ()
  "Show the jmaker customization global options panel."
  (interactive)
  (customize-group "jmaker"))

(defun jmaker-version-number ()
	"Returns jmaker version number."
	(string-match "[0123456789.]+" jmaker-version)
	(match-string 0 jmaker-version))

(defun jmaker-display-version ()
	"Displays jmaker version."
	(interactive)
	(message "Using 'jmaker' version %s." (jmaker-version-number)))

(defun jmaker-all-target ()
	"Returns a Makefile string for the `all' target. It is build with the name
of all `.java' files in the current directory. If the current directory contains
Sample1.java and Sample2.java `jmaker-all-target' returns:

  Sample1 \
  Sample2

"
	(let ((java-names (jmaker-get-java-names)))
		(apply 'concat
					 (mapcar '(lambda (name)
											(if (stringp name)
													(concat " \\\n\t" name)
												"\n"))
									 java-names))
		))

(defun jmaker-file-targets ()
	"Returns a Makefile string for java file targets. It is build with the name
of all `.java' files in the current directory. If the current directory contains
Sample1.java and Sample2.java `jmaker-file-targets' returns:

Sample1: Sample1.class
Sample2: Sample2.class

"
	(let ((java-names (jmaker-get-java-names)))
		(apply 'concat
					 (mapcar '(lambda (name)
											(if (stringp name)
													(concat name ":\t" name ".class\n")))
									 java-names))
		))

(defun jmaker-get-java-names ()
	"Returns a list of all java file names without extension in the current directory.
The last element of the list is the symbol `*end*'."
	(let ((java-files (directory-files default-directory nil ".\\.java$")))
		(nconc (mapcar 'file-name-sans-extension java-files)	'(*end*))))

(defun jmaker-generate-makefile ()
	"Generate a Makefile buffer to compile Java files in the current directory.
This is a temporary buffer which could be edited and must be saved to allow building."
	(interactive)
	(with-output-to-temp-buffer "Makefile"
		(set-buffer standard-output)
		(makefile-mode)
		(jmaker-insert-makefile)
		(switch-to-buffer standard-output)
		)
	)

(defun jmaker-jde-hook ()
	"JDE hook to add jmaker items to JDE sub-menus."
	;; jmaker JDE sub-menus are not supported by XEmacs!
	(when (and jmaker-hook-jde-menu (not jde-xemacsp))
		(define-key-after
			(lookup-key jde-mode-map [menu-bar JDE Options])
			[jmaker-opt] '("jmaker" . jmaker-customize)
			'Autocode)
		(define-key-after
			(lookup-key jde-mode-map [menu-bar JDE Generate])
			[jmaker-gen] '("Makefile" . jmaker-generate-makefile)
			'jmaker-gen)
		(define-key-after
			(lookup-key jde-mode-map [menu-bar JDE Help])
			[jmaker-ver] (list (concat "jmaker " (jmaker-version-number)))
			'jmaker-ver)
		)
	)

(if (and (>= emacs-major-version 20) (>= emacs-minor-version 3))
		(add-hook 'jde-mode-hook 'jmaker-jde-hook)
	(add-hook 'jde-mode-hooks 'jmaker-jde-hook))

(provide 'jmaker)

;;; Change History:

;;
;; $Log: jmaker.el,v $
;; Revision 1.5  1998/09/29 22:34:18  ebat311
;; New version-number management functions.
;;
;; Revision 1.4  1998/09/28 22:54:19  ebat311
;; Copyright notice updated.
;;
;; Revision 1.3  1998/09/28 22:18:58  ebat311
;; Fixed an error in function `jmaker-version-number' which did not display
;; the version number.
;;
;; Revision 1.2  1998/09/15 11:26:55  ebat311
;; Added support for Emacs 20.3.1.
;; From Paul Kinnucan: "Emacs 20.3.1 fixes a bug in the derive
;; mode macro that caused the jde-mode hook variable to be named
;; `jde-mode-hooks' (note plural) instead of `jde-mode-hook' (singular),
;; which is customary."
;;
;; Revision 1.1  1998/07/22 13:48:17  ebat311
;; Initial revision
;;
;;

;;; jmaker.el ends here.
